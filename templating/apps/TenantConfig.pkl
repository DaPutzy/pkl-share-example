class Item {
  // name of the tenant
  name: String

  // frontend app configuration
  frontend: ApplicationConfig?
}

class ApplicationConfig {

  // domain of the application
  domain: String

  // custom secret name of the application
  // this will disable acme!
  secret: String?

  // returns true if no custom secret is configured
  function useAcme(): Boolean = secret == null
}

tenants: Listing<Item>

configs: Object

local warningYaml = """
  ####################################################################################
  # ⚠️ THIS IS A GENERATED FILE ⚠️
  # DO NOT MAKE MANUAL CHANGES
  ####################################################################################


  """

local warningJson5 = """
  /*
    ⚠️ THIS IS A GENERATED FILE ⚠️
    DO NOT MAKE MANUAL CHANGES
  */


  """

output {
  text = if (renderer.extension == "yaml") warningYaml + super.text else
         if (renderer.extension == "json5") warningJson5 + super.text else
         super.text
}
